import WhiteheadTheorem.Auxiliary
import Mathlib.Topology.Homotopy.HomotopyGroup


open scoped unitInterval Topology Topology.Homotopy


namespace Cube

/-- `Cube.boundaryJar (n + 1) = âˆ‚Iâ¿ Ã— I âˆª Iâ¿ Ã— {0} âŠ† Iâ¿âºÂ¹` -/
def boundaryJar (n : â„•) : Set (I^ Fin n) :=
  match n with
  | 0 => âˆ…
  | _ + 1 => {y | (âˆƒ i, y i = 0 âˆ¨ y i = 1) âˆ§
      (y (Fin.last _) = 1 â†’ âˆƒ i < Fin.last _, y i = 0 âˆ¨ y i = 1)}

/-- `Cube.boundaryLid (n + 1) = Iâ¿ Ã— {1} âŠ† Iâ¿âºÂ¹` -/
def boundaryLid (n : â„•) : Set (I^ Fin n) :=
  match n with
  | 0 => âˆ…
  | _ + 1 => {y | y (Fin.last _) = 1}

scoped[Topology.Homotopy] notation "âˆ‚I^" n => Cube.boundary (Fin n)
scoped[Topology.Homotopy] notation "âŠ”I^" n => Cube.boundaryJar n

def boundaryInclusion (n : â„•) : C(âˆ‚I^n, I^ (Fin n)) where
  toFun := fun âŸ¨y, _âŸ© â†¦ y
  continuous_toFun := continuous_subtype_val.comp continuous_id
def boundaryJarInclusion (n : â„•) : C(âŠ”I^n, I^ (Fin n)) where
  toFun := fun âŸ¨y, _âŸ© â†¦ y
  continuous_toFun := continuous_subtype_val.comp continuous_id

instance isEmpty_boundary_zero : IsEmpty (âˆ‚I^0) :=
  Set.isEmpty_coe_sort.mpr <| Set.subset_empty_iff.mp fun _ âŸ¨i, _âŸ© â†¦ isEmptyElim i
instance isEmpty_boundaryJar_zero : IsEmpty (âŠ”I^0) := by
  rw [Set.isEmpty_coe_sort]; rfl

lemma boundaryJar_subset_boundary (n : â„•) : (âŠ”I^n) âŠ† (âˆ‚I^n) :=
  match n with
  | 0 => fun y hy â†¦ isEmptyElim (âŸ¨y, hyâŸ© : âŠ”I^0)
  | _ + 1 => fun _ âŸ¨hy1, _âŸ© â†¦ hy1

def boundaryJarInclusionToBoundary (n : â„•) : C(âŠ”I^n, âˆ‚I^n) where
  toFun := fun âŸ¨y, hyâŸ© â†¦ âŸ¨y, boundaryJar_subset_boundary n hyâŸ©
  continuous_toFun := by fun_prop

lemma mem_boundaryJar_of_lt_last {n : â„•} (y : I^ (Fin (n + 1)))
    (hy : âˆƒ i < Fin.last _, y i = 0 âˆ¨ y i = 1) : y âˆˆ âŠ”I^(n+1) := by
  obtain âŸ¨i, âŸ¨hi, hyiâŸ©âŸ© := hy
  constructor
  Â· exact âŸ¨i, hyiâŸ©
  Â· intro _; exact âŸ¨i, âŸ¨hi, hyiâŸ©âŸ©

lemma mem_boundaryJar_of_exists_eq_zero {n : â„•} (y : I^ Fin n)
    (hy : âˆƒ i, y i = 0) : y âˆˆ âŠ”I^n :=
  match n with
  | 0 => isEmptyElim hy.choose
  | n + 1 => by
      obtain âŸ¨i, hiâŸ© := hy
      constructor
      Â· use i; left; exact hi
      Â· intro hn1
        by_cases h : i = Fin.last _
        Â· rw [â† h] at hn1; exfalso; exact (by norm_num : (1 : I) â‰  0) (hn1.symm.trans hi)
        Â· use i; exact âŸ¨Fin.lt_last_iff_ne_last.mpr h, Or.inl hiâŸ©

lemma mem_boundaryLid_or_mem_boundaryJar_of_mem_boundary {n : â„•} (y : I^ Fin n)
    (hy : y âˆˆ âˆ‚I^n) : y âˆˆ Cube.boundaryLid n âˆ¨ y âˆˆ âŠ”I^n :=
  match n with
  | 0 => isEmptyElim (âŸ¨y, hyâŸ© : âˆ‚I^0)
  | n + 1 => by
      by_cases hyn : y (Fin.last _) = 1
      Â· left; exact hyn
      Â· right
        constructor
        Â· exact hy
        Â· intro hyn'; exfalso; exact hyn hyn'

/-- `âŠ”I^1 = {0}` is a singleton -/
instance uniqueBoundaryJarOne : Unique (âŠ”I^1) where
  default := âŸ¨0,
    âŸ¨ by use 0; simp only [Pi.zero_apply, zero_ne_one, or_false],
      by intro h; simp only [Pi.zero_apply, zero_ne_one] at h âŸ© âŸ©
  uniq := fun âŸ¨y, âŸ¨âŸ¨i, hiâŸ©, hy2âŸ©âŸ© â†¦ by
    ext j
    have : Unique (Fin 1) := by infer_instance
    have iz : i = 0 := Subsingleton.eq_zero i
    have jz : j = 0 := Subsingleton.eq_zero j
    rw [iz] at hi
    obtain h0 | h1 := hi
    all_goals simp only [Pi.zero_apply, Set.Icc.coe_zero, Set.Icc.coe_eq_zero]; rw [jz]
    Â· exact h0
    Â· exfalso; obtain âŸ¨k, hkâŸ© := hy2 h1; exact Nat.not_succ_le_zero k hk.left

def homeoNeqLast {n : â„•} : (I^ Fin n) â‰ƒâ‚œ I^{ j : Fin (n + 1) // j â‰  n } :=
  Homeomorph.piCongr
    { toFun i := âŸ¨i, by
        simp only [Fin.coe_eq_castSucc, Fin.natCast_eq_last, ne_eq]
        exact Fin.lt_last_iff_ne_last.mp i.2 âŸ©
      invFun i := âŸ¨i, by
        have := i.2
        simp only [ne_eq, Fin.natCast_eq_last] at this
        exact Fin.lt_last_iff_ne_last.mpr this âŸ©
      left_inv i := by simp only [Fin.coe_eq_castSucc, Fin.coe_castSucc, Fin.eta]
      right_inv i := by simp only [ne_eq, Fin.cast_val_eq_self, Subtype.coe_eta] }
    (fun _ â†¦ Homeomorph.refl _)

/-- A homeomorphism that sends `(yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚, yâ‚™)` to `(yâ‚™, (yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚))` -/
def splitAtLast {n : â„•} : (I^ Fin (n + 1)) â‰ƒâ‚œ I Ã— (I^ Fin n) :=
  splitAt (n : Fin (n + 1)) |>.trans <|
    Homeomorph.prodCongr (Homeomorph.refl _) homeoNeqLast.symm

/-- A homeomorphism that sends `(yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚, yâ‚™)` to `((yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚), yâ‚™)` -/
def splitAtLastComm {n : â„•} : (I^ Fin (n + 1)) â‰ƒâ‚œ (I^ Fin n) Ã— I :=
  splitAtLast.trans <| Homeomorph.prodComm I (I^ Fin n)

lemma splitAtLast_fst_eq {n : â„•} (y : I^ Fin (n + 1)) :
    (splitAtLast y).fst = y (Fin.last n) := by
  simp only [splitAtLast, ne_eq, Homeomorph.trans_apply, Homeomorph.funSplitAt_apply,
    Fin.natCast_eq_last, Homeomorph.coe_prodCongr, Homeomorph.refl_apply, Prod.map_apply, id_eq]

lemma splitAtLastComm_snd_eq {n : â„•} (y : I^ Fin (n + 1)) :
    (splitAtLastComm y).snd = y (Fin.last n) := by
  simp only [splitAtLastComm, splitAtLast, ne_eq, Homeomorph.trans_apply,
    Homeomorph.funSplitAt_apply, Fin.natCast_eq_last, Homeomorph.coe_prodCongr,
    Homeomorph.refl_apply, Prod.map_apply, id_eq, Homeomorph.coe_prodComm, Prod.swap_prod_mk]

lemma splitAtLast_snd_eq {n : â„•} (y : I^ Fin (n + 1)) :
    (splitAtLast y).snd = (splitAtLastComm y).fst := by
  simp only [splitAtLast, ne_eq, Homeomorph.trans_apply, Homeomorph.funSplitAt_apply,
    Fin.natCast_eq_last, Homeomorph.coe_prodCongr, Homeomorph.refl_apply, Prod.map_apply, id_eq,
    splitAtLastComm, Homeomorph.coe_prodComm, Prod.swap_prod_mk]

lemma splitAtLast_snd_apply_eq {n : â„•} (y : I^ Fin (n + 1)) (i : Fin n) :
    (splitAtLast y).snd i = y i.castSucc := by
  simp only [splitAtLast, ne_eq, homeoNeqLast, Fin.coe_eq_castSucc, Homeomorph.trans_apply,
    Homeomorph.funSplitAt_apply, Fin.natCast_eq_last, Homeomorph.coe_prodCongr,
    Homeomorph.refl_apply, Prod.map_apply, id_eq]
  rfl

lemma splitAtLast_symm_apply_eq_of_neq_last {n : â„•} (t : I) (y : I^ Fin n) (i : Fin (n + 1))
    (hi: i â‰  Fin.last _) :
    (splitAtLast.symm âŸ¨t, yâŸ©) i = y âŸ¨i, Fin.lt_last_iff_ne_last.mpr hiâŸ© := by
  simp only [splitAtLast, ne_eq, Homeomorph.symm_trans_apply, Homeomorph.prodCongr_symm,
    Homeomorph.refl_symm, Homeomorph.symm_symm, Homeomorph.coe_prodCongr, Homeomorph.refl_apply,
    Prod.map_apply, id_eq, Homeomorph.funSplitAt_symm_apply, Fin.natCast_eq_last, hi, â†“reduceDIte]
  simp only [homeoNeqLast, ne_eq, Fin.coe_eq_castSucc, Homeomorph.piCongr_apply,
    Equiv.coe_fn_symm_mk, Homeomorph.refl_apply, id_eq]

/-- `y âˆˆ âŠ”I^(n+1)` if and only if either `y` is on the bottom face,
or its first `n` coordinates constitute a point in `âˆ‚I^n`.
Note that `(splitAtLast y).fst` is the last (`n`-th) coordinate. -/
lemma mem_boundaryJar_iff_splitAtLast {n : â„•} {y : I^ Fin (n + 1)} :
    y âˆˆ (âŠ”I^(n+1)) â†” (splitAtLast y).fst = 0 âˆ¨ (splitAtLast y).snd âˆˆ âˆ‚I^n := by
  constructor
  . intro hy
    simp only [splitAtLast, ne_eq, Homeomorph.trans_apply, Homeomorph.funSplitAt_apply,
      Fin.natCast_eq_last, Homeomorph.coe_prodCongr, Homeomorph.refl_apply, Prod.map_apply, id_eq]
    by_cases h0 : y (Fin.last n) = 0
    Â· left; exact h0
    Â· right
      by_cases h1 : y (Fin.last n) = 1
      Â· have := hy.right h1
        obtain âŸ¨i, hi, hâŸ© := hy.right h1
        use âŸ¨i, hiâŸ©
        rcases h with h | h
        Â· left; change (homeoNeqLast.invFun _) _ = 0; simpa [homeoNeqLast]
        Â· right; change (homeoNeqLast.invFun _) _ = 1; simpa [homeoNeqLast]
      Â· obtain âŸ¨i, hâŸ© := hy.left
        have : i â‰  (Fin.last n) := fun hn â†¦ by
          rw [hn] at h; rcases h with h | h; exacts [h0 h, h1 h]
        use âŸ¨i.val, Fin.lt_last_iff_ne_last.mpr thisâŸ©
        rcases h with h | h
        Â· left; change (homeoNeqLast.invFun _) _ = 0; simpa [homeoNeqLast]
        Â· right; change (homeoNeqLast.invFun _) _ = 1; simpa [homeoNeqLast]
  . intro hy
    rcases hy with hy | âŸ¨i, hiâŸ©
    Â· rw [splitAtLast_fst_eq] at hy
      apply mem_boundaryJar_of_exists_eq_zero
      use Fin.last n
    Â· rw [splitAtLast_snd_apply_eq] at hi
      constructor
      Â· use i.castSucc
      Â· intro hyn
        use i.castSucc
        exact âŸ¨Fin.castSucc_lt_last i, hiâŸ©

/-- An easy corrolary of `mem_boundaryJar_iff_splitAtLast` -/
lemma splitAtLast_snd_mem_boundary_of_last_neq_zero {n : â„•} {y : I^ Fin (n + 1)}
    (hy : y âˆˆ âŠ”I^(n+1)) (hyn : y (Fin.last _) â‰  0) :
    (splitAtLast y).snd âˆˆ âˆ‚I^n := by
  rw [â† splitAtLast_fst_eq y] at hyn
  cases mem_boundaryJar_iff_splitAtLast.mp hy
  Â· exfalso; exact hyn â€¹_â€º
  Â· assumption

/-- The inclusion from the n-dimensional cube to the top face of the (n+1)-dimensional cube,
mapping (yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚) to (yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚, 1).
(Although `1` appears first in this definition, it is actually the last coordinate
in `(I^ Fin (n + 1))`, due to `Cube.insertAt`). -/
def inclToTop {n : â„•} : C(I^ Fin n, I^ Fin (n + 1)) where
  toFun y := splitAtLast.symm âŸ¨1, yâŸ©
  continuous_toFun := splitAtLast.symm.continuous.comp <|
    Continuous.prodMk continuous_const continuous_id

/-- (y, 1) is in the `boundary`. -/
lemma inclToTop.mem_boundary {n : â„•} (y : I^ Fin n) : inclToTop y âˆˆ âˆ‚I^(n + 1) := by
  use n
  right
  simp only [inclToTop, splitAtLast, ne_eq, Homeomorph.symm_trans_apply,
    Homeomorph.prodCongr_symm, Homeomorph.refl_symm, Homeomorph.symm_symm, Homeomorph.coe_prodCongr,
    Homeomorph.refl_apply, Prod.map_apply, id_eq, Fin.natCast_eq_last, ContinuousMap.coe_mk,
    Homeomorph.funSplitAt_symm_apply, â†“reduceDIte]

/-- If y is in the `boundary`, then (y, 1) is in the `boundaryJar`. -/
lemma inclToTop.mem_boundaryJar_of {n : â„•} {y : I^ Fin n}
    (hy : y âˆˆ âˆ‚I^n) : inclToTop y âˆˆ âŠ”I^(n + 1) := by
  obtain âŸ¨i, hiâŸ© := hy
  simp only [inclToTop, ContinuousMap.coe_mk]
  constructor
  Â· use n        -- the n-th coordinate of (y, 1) is 1
    simp only [splitAtLast, ne_eq, Fin.natCast_eq_last, Homeomorph.symm_trans_apply,
    Homeomorph.prodCongr_symm, Homeomorph.refl_symm, Homeomorph.symm_symm, Homeomorph.coe_prodCongr,
    Homeomorph.refl_apply, Prod.map_apply, id_eq, Homeomorph.funSplitAt_symm_apply, â†“reduceDIte,
    one_ne_zero, or_true]
  Â· intro _
    use i         -- the i-th coordinate of (y, 1) is 0 or 1, where i < n
    constructor
    Â· simp only [Fin.coe_eq_castSucc, Fin.natCast_eq_last, Fin.castSucc_lt_last]
    Â· simpa only [splitAtLast, ne_eq, homeoNeqLast, Fin.coe_eq_castSucc,
      Homeomorph.symm_trans_apply, Homeomorph.prodCongr_symm, Homeomorph.refl_symm,
      Homeomorph.symm_symm, Homeomorph.coe_prodCongr, Homeomorph.refl_apply, Prod.map_apply, id_eq,
      Homeomorph.funSplitAt_symm_apply, Fin.natCast_eq_last, Fin.castSucc_ne_last, â†“reduceDIte,
      Homeomorph.piCongr_apply, Equiv.coe_fn_symm_mk, Fin.coe_castSucc, Fin.eta]

lemma splitAtLast_inclToTop_eq {n : â„•} {y : I^ Fin n} :
    splitAtLast (inclToTop y) = âŸ¨1, yâŸ© := by
  simp only [splitAtLast, ne_eq, inclToTop, Homeomorph.symm_trans_apply,
    Homeomorph.prodCongr_symm, Homeomorph.refl_symm, Homeomorph.symm_symm, Homeomorph.coe_prodCongr,
    Homeomorph.refl_apply, Prod.map_apply, id_eq, ContinuousMap.coe_mk, Homeomorph.trans_apply,
    Homeomorph.apply_symm_apply, Homeomorph.symm_apply_apply]

/-- `(yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚, yâ‚™) â†¦ (yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚)` -/
def discardLast {n : â„•} : C(I^ Fin (n + 1), I^ Fin n) where
  toFun y := fun i â†¦ y âŸ¨i.val, i.prop.trans (by omega : n < n + 1)âŸ©
  continuous_toFun := by fun_prop

/-- (yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚) â†¦ (yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚, 0) -/
def inclToBot {n : â„•} : C(I^ Fin n, I^ Fin (n + 1)) where
  toFun y := Cube.insertAt (n : Fin (n + 1)) âŸ¨0, Cube.homeoNeqLast yâŸ©
  continuous_toFun := (Cube.insertAt _).continuous.comp <|
    Continuous.prodMk continuous_const Cube.homeoNeqLast.continuous

/-- (y, 0) is in the `boundary`. -/
lemma inclToBot.mem_boundary {n : â„•} (y : I^ Fin n) : inclToBot y âˆˆ âˆ‚I^(n + 1) := by
  use n
  left
  simp only [inclToBot, ne_eq, Fin.natCast_eq_last, ContinuousMap.coe_mk,
    Homeomorph.funSplitAt_symm_apply, â†“reduceDIte]

/-- (y, 0) is in the `boundaryJar`. -/
lemma inclToBot.mem_boundaryJar {n : â„•} (y : I^ Fin n) : inclToBot y âˆˆ âŠ”I^(n + 1) := by
  constructor
  Â· exact inclToBot.mem_boundary y
  Â· intro h; exfalso
    have : inclToBot y (Fin.last n) = (0 : â„) := by simp [inclToBot]
    refine (by norm_num : (0 : â„) â‰  (1 : â„)) <| this.symm.trans ?_
    rw [h, Set.Icc.coe_one]

/-- The inclusion (yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚) â†¦ (yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚, 0) to the bottom face of `âŠ”I^(n+1)` -/
def inclToBoundaryJarBot {n : â„•} : C(I^ Fin n, âŠ”I^(n+1)) where
  toFun y := âŸ¨ inclToBot y, inclToBot.mem_boundaryJar y âŸ©
  continuous_toFun := Continuous.subtype_mk inclToBot.continuous _

-- /-- The inclusion `(y, t) â†¦ (yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚, t)` to
-- the sides of `âŠ”I^(n+1)`, i.e.,
-- the closure of the complement of the top and bottom faces of `âˆ‚I^(n+1)`. -/
-- def inclToBoundaryJarSides {n : â„•} : C((âˆ‚I^n) Ã— I, âŠ”I^(n+1)) where
--   toFun := fun âŸ¨âŸ¨y, hyâŸ©, tâŸ© â†¦
--     âŸ¨ fun âŸ¨i, hiâŸ© â†¦
--         âŸ¨ if _ : i < n then y âŸ¨i, â€¹_â€ºâŸ© else t,
--           by split_ifs; repeat {simp only [Subtype.coe_prop]} âŸ©,
--       by
--         obtain âŸ¨âŸ¨i, hiâŸ©, hyiâŸ© := hy
--         constructor
--         Â· use âŸ¨i, hi.trans (by omega : n < n + 1)âŸ©; simp [hi, hyi]
--         Â· intro _
--           use âŸ¨i, hi.trans (by omega : n < n + 1)âŸ©; simpa [hi, hyi] âŸ©
--   continuous_toFun := by
--     refine Continuous.subtype_mk ?_ _
--     refine continuous_pi fun i â†¦ ?_
--     refine Continuous.subtype_mk ?_ _
--     split_ifs
--     Â· apply Continuous.subtype_val
--       exact continuous_apply (âŸ¨i.val, â€¹_â€ºâŸ© : Fin n) |>.comp <|
--         Continuous.subtype_val continuous_fst
--     Â· exact Continuous.subtype_val continuous_snd

/-- The inclusion `(y, t) â†¦ (yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚, t)` to
the sides of `âŠ”I^(n+1)`, i.e.,
the closure of the complement of the top and bottom faces of `âˆ‚I^(n+1)`. -/
def inclToBoundaryJarSides {n : â„•} : C((âˆ‚I^n) Ã— I, âŠ”I^(n+1)) where
  toFun := fun yt â†¦
    âŸ¨ (toContinuousMap splitAtLastComm.symm |>.comp <|
        ContinuousMap.prodMap (boundaryInclusion n) (ContinuousMap.id _)) yt,
    by
      obtain âŸ¨âŸ¨y, âŸ¨i, hyiâŸ©âŸ©, tâŸ© := yt
      constructor
      Â· use i.castSucc
        simp [splitAtLastComm, splitAtLast, homeoNeqLast, boundaryInclusion]
        simpa [Fin.castSucc_ne_last]
      Â· intro _; use i.castSucc
        simp [splitAtLastComm, splitAtLast, homeoNeqLast, boundaryInclusion]
        simpa [Fin.castSucc_ne_last, Fin.castSucc_lt_last]  âŸ©
  continuous_toFun := by
    refine Continuous.subtype_mk ?_ _
    simp only [ContinuousMap.coe_comp, ContinuousMap.coe_coe, Homeomorph.comp_continuous_iff]
    apply ContinuousMapClass.map_continuous

/-- The inclusion `(y, t) â†¦ (yâ‚€, yâ‚, â€¦, yâ‚™â‚‹â‚, t)` to the sides of
the $(n+1)$-dimensional cube. -/
def inclToSides {n : â„•} : C((âˆ‚I^n) Ã— I, I^ Fin (n + 1)) where
  toFun := Subtype.val âˆ˜ inclToBoundaryJarSides
  continuous_toFun := Continuous.subtype_val inclToBoundaryJarSides.continuous

end Cube


namespace TopCat

def cube (n : â„•) : TopCat.{u} := TopCat.of <| ULift <| I^ Fin n

def cubeBoundary (n : â„•) : TopCat.{u} := TopCat.of <| ULift <| Cube.boundary (Fin n)

def cubeBoundaryJar (n : â„•) : TopCat.{u} := TopCat.of <| ULift <| Cube.boundaryJar n

/-- `ğ•€ n` denotes the `n`-cube (as an object in `TopCat`). -/
scoped prefix:arg "ğ•€ " => cube

/-- `âˆ‚ğ•€ n` denotes the boundary of the `n`-cube (as an object in `TopCat`). -/
scoped prefix:arg "âˆ‚ğ•€ " => cubeBoundary

/-- `âŠ”ğ•€ n` denotes the "boundary jar" ($âŠ”Iâ¿âºÂ¹ = âˆ‚Iâ¿ Ã— I âˆª Iâ¿ Ã— {0} âŠ† Iâ¿âºÂ¹$)
of the `n`-cube (as an object in `TopCat`). -/
scoped prefix:arg "âŠ”ğ•€ " => cubeBoundaryJar

/-- The inclusion `âˆ‚ğ•€ n âŸ¶ ğ•€ n` of the boundary of the `n`-cube. -/
def cubeBoundaryInclusion (n : â„•) : cubeBoundary.{u} n âŸ¶ cube.{u} n :=
  ofHom
    { toFun := fun âŸ¨âŸ¨p, _âŸ©âŸ© â†¦ âŸ¨pâŸ©
      continuous_toFun :=
        continuous_uliftUp.comp <| continuous_subtype_val.comp continuous_induced_dom }

def cubeBoundaryJarInclusionToBoundary (n : â„•) : cubeBoundaryJar.{u} n âŸ¶ cubeBoundary.{u} n :=
  ofHom
    { toFun := fun âŸ¨pâŸ© â†¦ âŸ¨Cube.boundaryJarInclusionToBoundary n pâŸ©
      continuous_toFun := by fun_prop }

end TopCat
