import WhiteheadTheorem.Compressible.Defs
import WhiteheadTheorem.Shapes.MappingCylinder
import WhiteheadTheorem.HomotopyGroup.ChangeBasePt
import WhiteheadTheorem.RelHomotopyGroup.LongExactSeq
import WhiteheadTheorem.HEP.CubeJar
import Mathlib.Topology.Homotopy.Contractible

/-!
This file proves that if `f : C(X, Y)` is a weak homotopy equivalence,
then the inclusion map `MapCyl.domInclFromTop` from `X` to the mapping cylinder of `f`
is `n`-compressible for every natural number `n`, i.e.,
it is compressible with respect to `TopCat.diskBoundaryInclusion n : ‚àÇùîª n ‚ü∂ ùîª n`
for each `n`.
-/

open CategoryTheory TopCat
open scoped unitInterval ContinuousMap Topology Topology.Homotopy


section unique_pi_mapCyl

universe u

-- variable (n : ‚Ñï) {X Y : Type u} [TopologicalSpace X] [TopologicalSpace Y]
-- variable (x‚ÇÄ : X)
-- variable (f : C(X, Y))
variable (n : ‚Ñï) {X Y : TopCat.{u}} (f : X ‚ü∂ Y)
variable (x‚ÇÄ : X)

namespace HomotopyGroup

/-- If the map `œÄ‚Çô(X, x‚ÇÄ) ‚ü∂ œÄ‚Çô(Y, f x‚ÇÄ)` induced by `f` is an isomorphism,
then the map `œÄ‚Çô(X, x‚ÇÄ) ‚ü∂ œÄ‚Çô(MapCyl f, ‚ãØ)` induced by inclusion into the mapping cylinder
is an isomorphism. -/
lemma isIso_inducedPointedHom'_mapCyl_domIncl_of_isIso
    (hf : IsIso <| inducedPointedHom' n x‚ÇÄ f) :
    IsIso <| inducedPointedHom' n x‚ÇÄ (MapCyl.domIncl f) := by
  have f_i_r := inducedPointedHom'_comp_isoTarget_eq_comp n x‚ÇÄ (MapCyl.domIncl_retr_eq f).symm
  have iso_r : IsIso <| inducedPointedHom' n ((MapCyl.domIncl f).hom x‚ÇÄ) (MapCyl.retr f) := by
    apply isIso_inducedPointedHom'_of_isHomotopyEquiv
    exact MapCyl.isHomotopyEquiv_retr f
  replace f_i_r := (IsIso.comp_inv_eq _).mpr f_i_r
  rw [‚Üê f_i_r]
  infer_instance  -- `IsIso.comp_isIso` and `IsIso.inv_isIso`

/-- If the map `œÄ‚Çô(X, x‚ÇÄ) ‚ü∂ œÄ‚Çô(Y, f x‚ÇÄ)` induced by `f` is an isomorphism,
then the map `œÄ‚Çô(X, MapCyl.top f) ‚ü∂ œÄ‚Çô(MapCyl f, ‚ãØ)` induced by
the inclusion `domInclFromTop f : C(top f, MapCyl f)` is an isomorphism. -/
lemma isIso_inducedPointedHom_mapCyl_domInclFromTop_of_isIso
    (hf : IsIso <| inducedPointedHom' n x‚ÇÄ f) :
    IsIso <| inducedPointedHom n (MapCyl.domInclToTop f x‚ÇÄ) (MapCyl.domInclFromTop f) := by
  replace hf := isIso_inducedPointedHom'_mapCyl_domIncl_of_isIso _ _ _ hf
  have i_it_if := inducedPointedHom_comp_isoTarget_eq_comp n x‚ÇÄ
    (MapCyl.domIncl_hom_eq_domInclFromTop_comp_domInclToTop f)
  have iso_it : IsIso <| inducedPointedHom n x‚ÇÄ (MapCyl.domInclToTop f) := by
    apply HomotopyGroup.isIso_inducedPointedHom'_of_isHomeomorph
    exact MapCyl.isHomeomorph_domInclToTop f
  replace i_it_if := (IsIso.inv_comp_eq _).mpr i_it_if
  rw [‚Üê i_it_if]
  infer_instance

end HomotopyGroup


namespace RelHomotopyGroup

open HomotopyGroup

lemma inducedPointedHom_subtype_val_eq_iStar
    (n : ‚Ñï) {X : Type u} [TopologicalSpace X] (A : Set X) (a : A) :
    ‚áë(inducedPointedHom n a (‚ü®Subtype.val, continuous_subtype_val‚ü© : C(A, X))) =
      iStar n X A a :=
  rfl

-- lemma inducedPointedHom_domInclFromTop_eq_iStar :
--     ‚áë(inducedPointedHom n (MapCyl.domInclToTop f x‚ÇÄ) (MapCyl.domInclFromTop f)) =
--     iStar n (MapCyl f) (MapCyl.top f) (MapCyl.domInclToTop f x‚ÇÄ) := by
--   apply inducedPointedHom_subtype_val_eq_iStar

/-- If the map `œÄ‚Çô(X, x‚ÇÄ) ‚ü∂ œÄ‚Çô(Y, f x‚ÇÄ)` induced by `f` is an isomorphism,
then the map `iStar : œÄ‚Çô(X, MapCyl.top f) ‚Üí œÄ‚Çô(MapCyl f, ‚ãØ)` is bijective. -/
lemma bijective_iStar_mapCyl_of_isIso
    (hf : IsIso <| inducedPointedHom' n x‚ÇÄ f) :
    Function.Bijective <| iStar n (MapCyl f) (MapCyl.top f) (MapCyl.domInclToTop f x‚ÇÄ) := by
  rw [‚Üê inducedPointedHom_subtype_val_eq_iStar]
  apply (Pointed.isIso_iff_bijective _).mp
  apply isIso_inducedPointedHom_mapCyl_domInclFromTop_of_isIso
  exact hf

/-- If `f` is a weak homotopy equivalence, then the relative homotopy group
`œÄÔπç n (MapCyl f) (MapCyl.top f) (MapCyl.domInclToTop f x‚ÇÄ)` is zero for all `n ‚â• 1` and `x`. -/
theorem unique_pi_mapCyl_of_isWeakHomotopyEquiv (hf : IsWeakHomotopyEquiv f.hom) :
    Nonempty <| Unique <| œÄÔπç (n + 1) (MapCyl f) (MapCyl.top f) (MapCyl.domInclToTop f x‚ÇÄ) := by
  replace hf := isIso_inducedPointedHom_of_isWeakHomotopyEquiv hf
  exact ExactSeq.unique_mid_of_five
    (iStar (n + 1) _ _ _)
    (jStar (n + 1) _ _ _)
    (bd     n      _ _ _)
    (iStar  n      _ _ _)
    (bijective_iStar_mapCyl_of_isIso _ _ _ (hf (n + 1) _)).surjective
    (bijective_iStar_mapCyl_of_isIso _ _ _ (hf  n      _)).injective
    (isExactAt_iStar_jStar n _ _ _)
    (isExactAt_jStar_bd    n _ _ _)
    (isExactAt_bd_iStar    n _ _ _)

end RelHomotopyGroup

end unique_pi_mapCyl



namespace Cube

variable {n : ‚Ñï} (X : Type u) [TopologicalSpace X] (A : Set X)

/-- A continuous map from the `n`-dimensional cube to `X` is called a map of pairs to `(X, A)`
if it sends the boundary `‚àÇI^n` into `A`. -/
abbrev IsMapOfPairs (f : C(I^ Fin n, X)) : Prop := ‚àÄ y ‚àà ‚àÇI^n, f y ‚àà A

/-- For `n ‚â• 1`, if `f` is a continuous map of pairs from `(I^ Fin n, ‚àÇI^n)` to `(X, A)`,
then it is as a map of pairs homotopic to a `RelGenLoop`. -/
lemma exists_relGenLoop_homotopicWith_isMapOfPairs
    (f : C(I^ Fin (n + 1), X)) (hf : IsMapOfPairs X A f) :
    ‚àÉ a : A, ‚àÉ g : RelGenLoop (n + 1) X A a, f.HomotopicWith g fun h ‚Ü¶ IsMapOfPairs X A h := by
  let fb : C(‚àÇI^(n + 1), A) := ‚ü®fun y ‚Ü¶ ‚ü®f y, hf y y.property‚ü©, by fun_prop‚ü©
  let fj : C(‚äîI^(n + 1), A) := fb.comp <| boundaryJarInclusionToBoundary (n + 1)
  obtain ‚ü®y‚ÇÄ, Hfj‚ü© := contractible_iff_id_nullhomotopic (‚äîI^(n + 1)) |>.mp
    instContractibleSpaceBoundaryJar
  replace Hfj := Hfj.some.hcomp (ContinuousMap.Homotopy.refl fj)
  simp only [ContinuousMap.comp_id, ContinuousMap.comp_const] at Hfj
  let a‚ÇÄ : A := ‚ü®fj y‚ÇÄ, by
    change f y‚ÇÄ ‚àà A
    apply hf
    exact Cube.boundaryJar_subset_boundary _ y‚ÇÄ.property ‚ü©
  use a‚ÇÄ
  let fb' : C(‚àÇùïÄ (n + 1), A) := fb.comp ‚ü®ULift.down.{u}, continuous_uliftDown‚ü©
  let Hfj' : C((‚äîùïÄ (n + 1)) √ó I, A) := Hfj.toContinuousMap.argSwap.comp <|
    ContinuousMap.prodMap ‚ü®ULift.down.{u}, continuous_uliftDown‚ü© (ContinuousMap.id _)
  have : ‚áëfb' ‚àò (cubeBoundaryJarInclusionToBoundary (n + 1)).hom = ‚áëHfj' ‚àò fun x ‚Ü¶ (x, 0) := by
    unfold cubeBoundaryJarInclusionToBoundary boundaryJarInclusionToBoundary
    ext y
    simp only [ContinuousMap.coe_mk, hom_ofHom, Function.comp_apply]
    unfold fb' fb Hfj' ContinuousMap.argSwap
    simp only [ContinuousMap.comp_apply, ContinuousMap.coe_mk, ContinuousMap.comp_assoc,
      ContinuousMap.prodMap_apply, ContinuousMap.coe_id, Prod.map_apply, id_eq,
      ContinuousMap.prodSwap_apply, ContinuousMap.Homotopy.coe_toContinuousMap,
      ContinuousMap.Homotopy.apply_zero]
    rfl
  obtain ‚ü®H1, H1prop‚ü© := cubeBoundaryJarInclusionToBoundary_hasHEP n A fb' Hfj' this
  -- `fb : C(‚àÇI^(n + 1), X)` is homotopic through `H1` to a map that sends `‚äîI^(n + 1)` to `a‚ÇÄ`.
  let f' : C(ùïÄ (n + 1), X) := f.comp ‚ü®ULift.down.{u}, continuous_uliftDown‚ü©
  let H1' : C((‚àÇùïÄ (n + 1)) √ó I, X) := ContinuousMap.comp ‚ü®Subtype.val, continuous_subtype_val‚ü© H1
  have := cubeBoundaryInclusion_hasHEP (n + 1) X f'
  replace : ‚áëf' ‚àò (cubeBoundaryInclusion (n + 1)).hom = ‚áëH1' ‚àò fun x ‚Ü¶ (x, 0) := by
    unfold cubeBoundaryInclusion f' H1'
    ext ‚ü®y, hy‚ü©
    simp only [ContinuousMap.coe_comp, ContinuousMap.coe_mk, hom_ofHom, Function.comp_apply]
    change f y = _
    have := congr_fun H1prop.left ‚ü®‚ü®y, hy‚ü©‚ü©
    simp only [Function.comp_apply] at this
    rw [‚Üê this]
    rfl
  obtain ‚ü®H2, H2prop‚ü© := cubeBoundaryInclusion_hasHEP (n + 1) X f' H1' this
  -- `f : C(I^ Fin (n + 1), X)` is homotopic through `H2` to a map that
  -- sends `‚àÇI^(n + 1)` to `A` and `‚äîI^(n + 1)` to `a‚ÇÄ`.
  let g : C(I^ Fin (n + 1), X) := ‚ü®fun y ‚Ü¶ H2 ‚ü®‚ü®y‚ü©, 1‚ü©, by fun_prop‚ü©
  have gprop : g ‚àà RelGenLoop (n + 1) X A a‚ÇÄ := by
    unfold g
    constructor
    ¬∑ intro y hy
      simp only [ContinuousMap.coe_mk]
      have := congr_fun H2prop.right ‚ü®‚ü®y, hy‚ü©, 1‚ü©
      simp only [cubeBoundaryInclusion, hom_ofHom, Function.comp_apply, Prod.map_apply,
        id_eq] at this
      change _ = H2 ({ down := y }, 1) at this
      rw [‚Üê this]
      unfold H1'
      simp only [ContinuousMap.comp_apply, ContinuousMap.coe_mk, Subtype.coe_prop]
    ¬∑ intro y hy
      have hy' := boundaryJar_subset_boundary _ hy
      simp only [ContinuousMap.coe_mk]
      have := congr_fun H2prop.right ‚ü®‚ü®y, hy'‚ü©, 1‚ü©
      simp only [cubeBoundaryInclusion, hom_ofHom, Function.comp_apply, Prod.map_apply,
        id_eq] at this
      change _ = H2 ({ down := y }, 1) at this
      rw [‚Üê this]
      replace := congr_fun H1prop.right ‚ü®‚ü®y, hy‚ü©, 1‚ü©
      simp only [cubeBoundaryJarInclusionToBoundary, boundaryJarInclusionToBoundary,
        ContinuousMap.coe_mk, hom_ofHom, Function.comp_apply, Prod.map_apply, id_eq] at this
      change _ = H1 ({ down := ‚ü®y, hy'‚ü© }, 1) at this
      unfold H1'
      simp only [ContinuousMap.comp_apply, ContinuousMap.coe_mk]
      rw [‚Üê this]
      unfold Hfj' ContinuousMap.argSwap a‚ÇÄ
      simp only [ContinuousMap.coe_mk, ContinuousMap.comp_assoc, ContinuousMap.comp_apply,
        ContinuousMap.prodMap_apply, ContinuousMap.coe_id, Prod.map_apply, id_eq,
        ContinuousMap.prodSwap_apply, ContinuousMap.Homotopy.coe_toContinuousMap,
        ContinuousMap.Homotopy.apply_one, ContinuousMap.const_apply]
  use ‚ü®g, gprop‚ü©
  exact Nonempty.intro <|
    { toContinuousMap := H2.argSwap.comp <|
        (ContinuousMap.id _).prodMap ‚ü®ULift.up, continuous_uliftUp‚ü©
      map_zero_left y := by
        unfold ContinuousMap.argSwap
        simp only [ContinuousMap.coe_mk, ContinuousMap.comp_assoc, ContinuousMap.toFun_eq_coe,
          ContinuousMap.comp_apply, ContinuousMap.prodMap_apply, ContinuousMap.coe_id,
          Prod.map_apply, id_eq, ContinuousMap.prodSwap_apply]
        have := congr_fun H2prop.left ‚ü®y‚ü©
        change _ = H2 ({ down := y }, 0)  at this
        rw [‚Üê this]
        rfl
      map_one_left y := by
        unfold ContinuousMap.argSwap g
        simp only [ContinuousMap.coe_mk, ContinuousMap.comp_assoc, ContinuousMap.toFun_eq_coe,
          ContinuousMap.comp_apply, ContinuousMap.prodMap_apply, ContinuousMap.coe_id,
          Prod.map_apply, id_eq, ContinuousMap.prodSwap_apply]
      prop' t y hy := by
        unfold ContinuousMap.argSwap
        simp only [ContinuousMap.coe_mk, ContinuousMap.comp_assoc, ContinuousMap.toFun_eq_coe,
          ContinuousMap.comp_apply, ContinuousMap.prodMap_apply, ContinuousMap.coe_id,
          Prod.map_apply, id_eq, ContinuousMap.prodSwap_apply]
        have := congr_fun H2prop.right ‚ü®‚ü®y, hy‚ü©, t‚ü©
        simp only [ContinuousMap.comp_apply, ContinuousMap.coe_mk, cubeBoundaryInclusion, hom_ofHom,
          Function.comp_apply, Prod.map_apply, id_eq, H1'] at this
        change _ = H2 ({ down := y }, t) at this
        rw [‚Üê this]
        exact Subtype.coe_prop (H1 ({ down := ‚ü®y, hy‚ü© }, t)) }

lemma homotopicWith_isMapOfPairs_of_relGenLoop_homotopic
    {X : Type u} [TopologicalSpace X] {A : Set X}
    {a : A} {f g : RelGenLoop n X A a} (fg : RelGenLoop.Homotopic f g) :
    f.val.HomotopicWith g.val fun h ‚Ü¶ IsMapOfPairs X A h := by
  replace fg := fg.some
  exact Nonempty.intro <|
    { toHomotopy := fg.toHomotopy
      prop' t y hy := (fg.prop' t).left y hy }

/-- Suppose `n ‚â• 1` and the relative homotopy group `œÄÔπç n X A a` is zero for all `a : A`.
If `f` is a continuous map of pairs from `(I^ Fin n, ‚àÇI^n)` to `(X, A)`,
then it is as a map of pairs homotopic to a constant map. -/
theorem homotopicWith_const_isMapOfPairs_of_unique_pi
    (f : C(I^ Fin (n + 1), X)) (hf : IsMapOfPairs X A f)
    (hpi : ‚àÄ a : A, Nonempty <| Unique <| œÄÔπç (n + 1) X A a) :
    ‚àÉ a : A, f.HomotopicWith (ContinuousMap.const _ a) fun h ‚Ü¶ IsMapOfPairs X A h := by
  obtain ‚ü®a, g, H‚ü© := exists_relGenLoop_homotopicWith_isMapOfPairs X A f hf
  have g0 := (hpi a |>.some.uniq ‚ü¶g‚üß).trans (hpi a |>.some.uniq ‚ü¶RelGenLoop.const‚üß).symm
  rw [Quotient.eq] at g0
  change RelGenLoop.Homotopic .. at g0
  use a
  exact H.trans <| homotopicWith_isMapOfPairs_of_relGenLoop_homotopic g0

end Cube


namespace TopCat.disk

open TopCat

variable {n : ‚Ñï} (X : Type u) [TopologicalSpace X] (A : Set X)

/-- A continuous map from the `n`-dimensional disk to `X` is called a map of pairs to `(X, A)`
if it sends the boundary `‚àÇùîª n` into `A`. -/
abbrev IsMapOfPairs (f : C(ùîª n, X)) : Prop := ‚àÄ y : ‚àÇùîª n, f (diskBoundaryInclusion n y) ‚àà A

/-- Suppose `n ‚â• 1` and the relative homotopy group `œÄÔπç n X A a` is zero for all `a : A`.
If `f` is a continuous map of pairs from `(‚àÇùîª n, ùîª n)` to `(X, A)`,
then it is as a map of pairs homotopic to a constant map. -/
theorem homotopicWith_const_isMapOfPairs_of_unique_pi
    (f : C(disk.{u} (n + 1), X)) (hf : IsMapOfPairs X A f)
    (hpi : ‚àÄ a : A, Nonempty <| Unique <| œÄÔπç (n + 1) X A a) :
    ‚àÉ a : A, f.HomotopicWith (ContinuousMap.const _ a) fun h ‚Ü¶ IsMapOfPairs X A h := by
  let e := diskPair.homeoCubePairULift.{u} (n + 1)
  let idown : C(ùïÄ (n + 1), I^ Fin (n + 1)) := ‚ü®ULift.down.{u}, continuous_uliftDown‚ü©
  let iup : C(I^ Fin (n + 1), ùïÄ (n + 1)) := ‚ü®ULift.up.{u}, continuous_uliftUp‚ü©
  let i_d : C(I^ Fin (n + 1), ùîª (n + 1)) := e.inv.right.hom.comp iup
  let d_i : C(ùîª (n + 1), I^ Fin (n + 1)) := idown.comp e.hom.right.hom
  let f' : C(I^ Fin (n + 1), X) := f.comp i_d
  have hf' : Cube.IsMapOfPairs X A f' := fun y hy ‚Ü¶ by
    unfold f' i_d iup
    simp only [Arrow.mk_right, ContinuousMap.comp_apply, ContinuousMap.coe_mk]
    change f ( (cubeBoundaryInclusion (n + 1) ‚â´ e.inv.right) ‚ü®‚ü®y, hy‚ü©‚ü© ) ‚àà A
    change f ( (e.inv.left ‚â´ diskBoundaryInclusion (n + 1)) ‚ü®‚ü®y, hy‚ü©‚ü© ) ‚àà A
    change f ( diskBoundaryInclusion (n + 1) <| e.inv.left ‚ü®‚ü®y, hy‚ü©‚ü© ) ‚àà A  -- CategoryTheory.Arrow.iso_w e
    apply hf
  obtain ‚ü®a, H‚ü© := Cube.homotopicWith_const_isMapOfPairs_of_unique_pi X A f' hf' hpi
  use a
  replace H := H.some
  let H' := (ContinuousMap.Homotopy.refl d_i).hcomp H.toHomotopy
  have f'_d_i : f'.comp d_i = f := by
    unfold f' d_i i_d
    simp only [Arrow.mk_right, ContinuousMap.comp_assoc]
    change _ = f.comp (ContinuousMap.id _)
    congr 1
    change e.inv.right.hom.comp ((iup.comp idown).comp e.hom.right.hom) = _
    rw [(by rfl : iup.comp idown = ContinuousMap.id _), ContinuousMap.id_comp]
    change (e.hom.right ‚â´ e.inv.right).hom = _
    simp only [Arrow.mk_right, Arrow.hom_inv_id_right, hom_id]
  exact Nonempty.intro <|
    { toContinuousMap := H'.toContinuousMap
      map_zero_left x := by rw [H'.map_zero_left x, f'_d_i]
      map_one_left x := by rw [H'.map_one_left x]; rfl
      prop' t x := by
        unfold H' d_i diskBoundaryInclusion
        simp only [Arrow.mk_right, ContinuousMap.toFun_eq_coe,
          ContinuousMap.Homotopy.coe_toContinuousMap, ContinuousMap.Homotopy.hcomp_apply,
          ContinuousMap.Homotopy.refl_apply, ContinuousMap.comp_apply,
          ContinuousMap.HomotopyWith.coe_toHomotopy, hom_ofHom, ContinuousMap.coe_mk]
        apply H.prop' t
        change idown ((diskBoundaryInclusion (n + 1) ‚â´ e.hom.right) x) ‚àà _
        change idown ((e.hom.left ‚â´ cubeBoundaryInclusion (n + 1)) x) ‚àà _  -- diskPair.homeoCubePairULift_comm
        change idown (cubeBoundaryInclusion (n + 1) (e.hom.left x)) ‚àà ‚àÇI^n + 1
        have : ‚àÄ z, idown (cubeBoundaryInclusion (n + 1) z) ‚àà ‚àÇI^n + 1 := by
          intro ‚ü®z, hz‚ü©
          unfold idown cubeBoundaryInclusion
          simp only [hom_ofHom, ContinuousMap.coe_mk]
          simp_all only [Subtype.forall, Arrow.mk_right, ContinuousMap.comp_assoc, f', i_d, e, iup, d_i, idown]
          obtain ‚ü®val, property‚ü© := a
          obtain ‚ü®val_1, property_1‚ü© := t
          simp_all only [Set.mem_Icc]
          obtain ‚ü®left, right‚ü© := property_1
          exact hz
        apply this }

noncomputable def _root_.TopCat.Cyl.stretchToWall :
    C(I √ó (disk.{u} (n + 1)), I √ó (disk.{u} (n + 1))) := by
  let Œ≤ : C((disk.{u} (n + 1)) √ó I, ‚Ñù) :=
    { toFun := fun ‚ü®‚ü®x, hx‚ü©, t‚ü© ‚Ü¶ max (2 * ‚Äñx‚Äñ) (2 - t)
      continuous_toFun := by fun_prop }
  refine
    { toFun := fun ‚ü®t, ‚ü®x, hx‚ü©‚ü© ‚Ü¶
        ‚ü® ‚ü®2 - Œ≤ ‚ü®‚ü®x, hx‚ü©, t‚ü©, ?_‚ü©, ‚ü®(2 / Œ≤ ‚ü®‚ü®x, hx‚ü©, t‚ü©) ‚Ä¢ x, ?_‚ü© ‚ü©
      continuous_toFun := ?_ }
  ¬∑ simp only [ContinuousMap.coe_mk, Set.mem_Icc, sub_nonneg, sup_le_iff, Nat.ofNat_pos,
    mul_le_iff_le_one_right, tsub_le_iff_right, le_add_iff_nonneg_right, Œ≤]
    have t1 : 1 ‚â§ ((2 : ‚Ñù) - t.val) := by linarith only [t.property.right]
    constructor
    ¬∑ constructor
      ¬∑ have := mem_closedBall_iff_norm.mp hx
        simp_all only [Metric.mem_closedBall, dist_zero_right, sub_zero]
      ¬∑ exact t.property.left
    ¬∑ by_cases hxt : 2 * ‚Äñx‚Äñ ‚â• 2 - t
      ¬∑ simp only [hxt, sup_of_le_left]
        have := t1.trans hxt
        replace := (add_le_add_iff_left 1).mpr this
        convert this
        norm_num
      ¬∑ replace hxt := le_of_not_ge hxt
        simp only [hxt, sup_of_le_right, ge_iff_le]
        convert (add_le_add_iff_left 1).mpr t1
        norm_num
  ¬∑ simp only [ContinuousMap.coe_mk, Metric.mem_closedBall, dist_zero_right, Œ≤]
    by_cases hxt : 2 * ‚Äñx‚Äñ ‚â• 2 - t
    ¬∑ simp only [hxt, sup_of_le_left]
      rw [div_mul_cancel_left‚ÇÄ (by norm_num : (2 : ‚Ñù) ‚â† 0)]
      rw [norm_smul, norm_inv, norm_norm]
      exact inv_mul_le_one
    ¬∑ replace hxt := le_of_not_ge hxt
      simp only [hxt, sup_of_le_right, ge_iff_le]
      rw [norm_smul]
      replace hxt := le_div_iff‚ÇÄ' (by norm_num : (2 : ‚Ñù) > 0) |>.mpr hxt
      replace hxt := mul_le_mul_of_nonneg_left hxt (norm_nonneg _ : ‚Äñ(2 : ‚Ñù) / (2 - t)‚Äñ ‚â• 0)
      convert hxt
      have : ‚Äñ(2 : ‚Ñù) / (2 - t)‚Äñ = (2 : ‚Ñù) / (2 - t) := by
        apply Real.norm_of_nonneg
        exact div_nonneg (by norm_num : (0 : ‚Ñù) ‚â§ 2) (by linarith only [t.property.right])
      rw [this]
      simp only [ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, div_mul_div_cancel‚ÇÄ']
      apply Eq.symm
      apply div_self
      linarith only [t.property.right]
  ¬∑ simp only [ContinuousMap.coe_mk, Œ≤]
    apply Continuous.prodMk
    ¬∑ apply Continuous.subtype_mk
      apply Continuous.sub
      ¬∑ exact continuous_const
      ¬∑ fun_prop
    ¬∑ apply continuous_uliftUp.comp
      apply Continuous.subtype_mk
      apply Continuous.smul
      ¬∑ apply Continuous.div
        ¬∑ exact continuous_const
        ¬∑ apply Continuous.max
          ¬∑ fun_prop
          ¬∑ fun_prop
        ¬∑ intro ‚ü®t, ‚ü®x, hx‚ü©‚ü©
          dsimp only
          have t1 : 0 < ((2 : ‚Ñù) - t.val) := by linarith only [t.property.right]
          apply ne_of_gt
          exact lt_sup_of_lt_right t1
      ¬∑ fun_prop

lemma _root_.TopCat.Cyl.stretchToWall_eq_zero_of_norm_eq_one
    {n : ‚Ñï} {t : I} {x : EuclideanSpace ‚Ñù (Fin (n + 1))} {hx : x ‚àà Metric.closedBall 0 1}
    (hx1 : ‚Äñx‚Äñ = 1) : Cyl.stretchToWall ‚ü®t, ‚ü®x, hx‚ü©‚ü© = ‚ü®0, ‚ü®x, hx‚ü©‚ü© := by
  unfold Cyl.stretchToWall
  simp only [ContinuousMap.coe_mk, hx1, mul_one, Prod.mk.injEq]
  have : 2 ‚â• 2 - t.val := by linarith only [t.property.left]
  simp only [this, sup_of_le_left, sub_self, Set.Icc.mk_zero, ne_eq, OfNat.ofNat_ne_zero,
    not_false_eq_true, div_self, one_smul, and_self]

/-- Suppose `n ‚â• 1` and `f` is a continuous map of pairs from `(‚àÇùîª n, ùîª n)` to `(X, A)`.
If `f` is as a map of pairs homotopic to a map into `A`,
then `f` is relative to `‚àÇùîª n` homotopic to a map into `A`. -/
theorem homotopicRel_boundary_of_homotopicWith_isMapOfPairs
    (f : C(disk.{u} (n + 1), X))  -- (hf : IsMapOfPairs X A f)
    (H : ‚àÉ g : C(disk.{u} (n + 1), X),
      Set.range g ‚äÜ A ‚àß f.HomotopicWith g fun h ‚Ü¶ IsMapOfPairs X A h) :
    ‚àÉ l : C(disk.{u} (n + 1), X),
      Set.range l ‚äÜ A ‚àß f.HomotopicRel l (Set.range (diskBoundaryInclusion _)) := by
  obtain ‚ü®g, gA, H‚ü© := H
  replace H := H.some
  let H' := H.toContinuousMap.comp Cyl.stretchToWall
  let l : C(disk.{u} (n + 1), X) := ‚ü®fun x ‚Ü¶ H' ‚ü®1, x‚ü©, by fun_prop‚ü©
  use l
  constructor
  ¬∑ apply Set.range_subset_iff.mpr
    intro ‚ü®x, hx‚ü©
    unfold l H' Cyl.stretchToWall
    simp only [ContinuousMap.coe_mk, ContinuousMap.comp_apply,
      ContinuousMap.Homotopy.coe_toContinuousMap, ContinuousMap.HomotopyWith.coe_toHomotopy,
      Set.Icc.coe_one]
    simp only [(by norm_num : (2 : ‚Ñù) - 1 = 1)]
    by_cases hx1 : 2 * ‚Äñx‚Äñ ‚â• 1
    ¬∑ simp only [hx1, sup_of_le_left]
      simp only [div_mul_cancel_left‚ÇÄ (by norm_num : (2 : ‚Ñù) ‚â† 0)]
      generalize_proofs pf1 pf2
      have xmem : ‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x ‚àà Metric.sphere 0 1 := by
        apply Metric.mem_sphere.mpr
        rw [dist_eq_norm, sub_zero, norm_smul, norm_inv, norm_norm]
        apply inv_mul_cancel‚ÇÄ
        linarith only [hx1]
      have : diskBoundaryInclusion.{u} (n + 1) ‚ü®‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x, xmem‚ü© = ‚ü®‚ü®‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x, pf2‚ü©‚ü© := rfl
      rw [‚Üê this]
      have := H.prop' ‚ü®2 - 2 * ‚Äñx‚Äñ, pf1‚ü© ‚ü®‚ü®‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x, xmem‚ü©‚ü©
      simp only [ContinuousMap.toFun_eq_coe, ContinuousMap.Homotopy.coe_toContinuousMap,
        ContinuousMap.HomotopyWith.coe_toHomotopy, ContinuousMap.coe_mk] at this
      exact this
    ¬∑ replace hx1 := le_of_not_ge hx1
      simp only [hx1, sup_of_le_right, div_one]
      simp only [(by norm_num : (2 : ‚Ñù) - 1 = 1)]
      change H (1, _) ‚àà A
      rw [H.apply_one]
      apply gA
      simp_all only [Set.mem_range, exists_apply_eq_apply]
  ¬∑ exact Nonempty.intro <|
      { toContinuousMap := H'
        map_zero_left := fun ‚ü®x, hx‚ü© ‚Ü¶ by
          unfold H' Cyl.stretchToWall
          simp only [ContinuousMap.coe_mk, ContinuousMap.toFun_eq_coe, ContinuousMap.comp_apply,
            Set.Icc.coe_zero, sub_zero, ContinuousMap.Homotopy.coe_toContinuousMap,
            ContinuousMap.HomotopyWith.coe_toHomotopy]
          have : 2 * ‚Äñx‚Äñ ‚â§ 2 := by
            have := Metric.mem_closedBall.mp hx
            rw [dist_eq_norm, sub_zero] at this
            linarith only [this]
          simp only [this, sup_of_le_right, sub_self, Set.Icc.mk_zero, ne_eq, OfNat.ofNat_ne_zero,
            not_false_eq_true, div_self, one_smul, ContinuousMap.HomotopyWith.apply_zero]
        map_one_left x := by unfold l; rfl
        prop' t x hy := by
          unfold H'
          simp only [ContinuousMap.toFun_eq_coe, ContinuousMap.comp_apply,
            ContinuousMap.Homotopy.coe_toContinuousMap, ContinuousMap.HomotopyWith.coe_toHomotopy,
            ContinuousMap.coe_mk]
          obtain ‚ü®x, hx‚ü© := x
          obtain ‚ü®‚ü®y, hy‚ü©, hy'‚ü© := Set.mem_range.mp hy
          simp only [diskBoundaryInclusion, hom_ofHom] at hy'
          replace hy' := (congr_arg (Subtype.val ‚àò ULift.down) hy')
          simp only [Function.comp_apply] at hy'
          have hx1 : ‚Äñx‚Äñ = 1 := by
            rw [‚Üê hy']
            change ‚Äñy‚Äñ = 1
            convert Metric.mem_sphere.mp hy using 1
            exact Eq.symm (dist_zero_right y)
          rw [Cyl.stretchToWall_eq_zero_of_norm_eq_one hx1]
          rw [H.apply_zero] }

/-- Suppose `n ‚â• 1` and the relative homotopy group `œÄÔπç n X A a` is zero for all `a : A`.
If `f` is a continuous map of pairs from `(‚àÇùîª n, ùîª n)` to `(X, A)`,
then it is relative to `‚àÇùîª n` homotopic to a map into `A`. -/
theorem homotopicRel_boundary_of_unique_pi
    (f : C(disk.{u} (n + 1), X)) (hf : IsMapOfPairs X A f)
    (hpi : ‚àÄ a : A, Nonempty <| Unique <| œÄÔπç (n + 1) X A a) :
    ‚àÉ l : C(disk.{u} (n + 1), X),
      Set.range l ‚äÜ A ‚àß f.HomotopicRel l (Set.range (diskBoundaryInclusion _)) := by
  obtain ‚ü®a, H‚ü© := homotopicWith_const_isMapOfPairs_of_unique_pi X A f hf hpi
  let g : C(disk.{u} (n + 1), X) := ContinuousMap.const (ùîª (n + 1)) a
  have gr : Set.range g ‚äÜ A := by
    unfold g
    intro x hx
    obtain ‚ü®y, hy‚ü© := Set.mem_range.mp hx
    simp only [ContinuousMap.const_apply] at hy
    subst hy
    simp_all only [ContinuousMap.coe_const, Set.mem_range, Function.const_apply, exists_const_iff, and_true,
      Subtype.coe_prop]
  apply homotopicRel_boundary_of_homotopicWith_isMapOfPairs X A
  use g

/-- For `n ‚â• 1`, if the relative homotopy group `œÄÔπç (n + 1) X A a` is zero
(regardless of the basepoint `a`), then the inclusion map form `A` to `X` is `n`-compressible. -/
theorem isCompressible_subtype_val_of_unique_pi
    (n : ‚Ñï) (X : Type u) [TopologicalSpace X] (A : Set X)
    (hpi : ‚àÄ a : A, Nonempty <| Unique <| œÄÔπç (n + 1) X A a) :
    IsCompressible (diskBoundaryInclusion (n + 1))
      (ofHom ‚ü®Subtype.val, continuous_subtype_val‚ü© : of A ‚ü∂ of X) where
  sq_hasLift := fun {F f} sq ‚Ü¶ by
    constructor
    have F_pair : disk.IsMapOfPairs X A F.hom := fun x ‚Ü¶ by
      change (diskBoundaryInclusion (n + 1) ‚â´ F) x ‚àà A
      rw [‚Üê sq.w]
      simp only [hom_comp, hom_ofHom, ContinuousMap.comp_apply, ContinuousMap.coe_mk,
        Subtype.coe_prop]
    obtain ‚ü®l, lA, H‚ü© := disk.homotopicRel_boundary_of_unique_pi X A F.hom F_pair hpi
    replace lA := Set.range_subset_iff.mp lA
    let l' :  C(disk.{u} (n + 1), A) := ‚ü®fun x ‚Ü¶ ‚ü®l x, lA x‚ü©, by fun_prop‚ü©
    refine Nonempty.intro ‚ü®ofHom l', ?_, ?_‚ü©
    ¬∑ ext x
      unfold l'
      simp only [hom_comp, hom_ofHom, ContinuousMap.comp_apply, ContinuousMap.coe_mk]
      let x' := diskBoundaryInclusion (n + 1) x
      have x'r : x' ‚àà Set.range (diskBoundaryInclusion (n + 1)) := Set.mem_range_self x
      have := H.some.prop' 1 x' x'r
      simp only [ContinuousMap.toFun_eq_coe, ContinuousMap.Homotopy.coe_toContinuousMap,
        ContinuousMap.Homotopy.apply_one, ContinuousMap.coe_mk] at this
      convert this
      unfold x'
      change _ = (diskBoundaryInclusion (n + 1) ‚â´ F) x
      rw [‚Üê sq.w]
      simp only [hom_comp, hom_ofHom, ContinuousMap.comp_apply, ContinuousMap.coe_mk]
    ¬∑ convert H

open RelHomotopyGroup in
/-- If `iStar : œÄ_ 0 A pt ‚Üí œÄ_ 0 X pt` is bijective (for some basepoint `pt`, which is irrelevant),
then the inclusion map from `A` to `X` is `0`-compressible. -/
theorem isCompressible_zero_subtype_val_of_bijective_iStar_zero
    (X : Type u) [TopologicalSpace X] (A : Set X) (pt : A)
    (hbi : Function.Bijective <| iStar 0 X A pt) :
    IsCompressible (diskBoundaryInclusion 0)
      (ofHom ‚ü®Subtype.val, continuous_subtype_val‚ü© : of A ‚ü∂ of X) := by
  constructor
  intro F f sq
  have xD0 : 0 ‚àà Metric.closedBall (0 : EuclideanSpace ‚Ñù (Fin 0)) 1 := by
    simp only [Metric.mem_closedBall, dist_self, zero_le_one]
  let x : X := F.hom ‚ü®0, xD0‚ü©
  let Œ≤' : GenLoop (Fin 0) X pt :=
    ‚ü®ContinuousMap.const _ x, fun y hy ‚Ü¶ isEmptyElim (‚ü®y, hy‚ü© : ‚àÇI^0)‚ü©
  let Œ≤ : œÄ_ 0 X pt := ‚ü¶Œ≤'‚üß
  obtain ‚ü®Œ±, iŒ±‚ü© := hbi.surjective Œ≤
  let Œ±' := Œ±.out
  replace iŒ± : iStar 0 X A pt ‚ü¶Œ±'‚üß = ‚ü¶Œ≤'‚üß := by unfold Œ±'; rwa [Quotient.out_eq]
  change iStar' .. = _ at iŒ±
  have H : ContinuousMap.HomotopicRel .. := Quotient.eq.mp iŒ±.symm
  replace H := H.some
  let a : X := H ‚ü®1, ![]‚ü©
  have aA : a ‚àà A := by
    unfold a
    rw [H.apply_one ![]]
    simp_all only [ContinuousMap.coe_mk, Function.comp_apply, Subtype.coe_prop, Œ±', Œ≤', x]
  have := H.toContinuousMap
  let l : C(disk.{u} 0, A) := ContinuousMap.const _ ‚ü®a, aA‚ü©
  constructor
  refine ‚ü®ofHom l, ?_, ?_‚ü©
  ¬∑ ext x
    exact isEmptyElim x
  ¬∑ exact Nonempty.intro
      { toFun := fun ‚ü®t, _‚ü© ‚Ü¶ H.toContinuousMap ‚ü®t, ![]‚ü©
        continuous_toFun := by fun_prop
        map_zero_left y := by
          unfold Œ≤'
          simp only [ContinuousMap.const_apply, ContinuousMap.Homotopy.coe_toContinuousMap,
            ContinuousMap.Homotopy.apply_zero]
          unfold x
          congr
          have u : Unique <| EuclideanSpace ‚Ñù (Fin 0) := by infer_instance
          convert u.uniq 0
          exact u.uniq _
        map_one_left y := by
          unfold l a
          simp only [ContinuousMap.Homotopy.coe_toContinuousMap, ContinuousMap.Homotopy.apply_one,
            ContinuousMap.coe_mk, Function.comp_apply, ContinuousMap.HomotopyWith.apply_one,
            Subtype.coe_eta, hom_comp, hom_ofHom, ContinuousMap.comp_const,
            ContinuousMap.const_apply]
        prop' t x := by
          simp only [Set.mem_range, IsEmpty.exists_iff, ContinuousMap.Homotopy.coe_toContinuousMap,
            ContinuousMap.HomotopyWith.coe_toHomotopy, ContinuousMap.coe_mk, IsEmpty.forall_iff] }

/-- If `œÜ` is a weak homotopy equivalence,
then the inclusion map `MapCyl.domInclFromTop œÜ`
from the top surface of the mapping cylinder of `œÜ` to the mapping cylinder of `œÜ`
is `n`-compressible for each natural number `n`. -/
lemma isCompressible_mapcyl_domInclFromTop_of_isWeakHomotopyEquiv
    (n : ‚Ñï) {X Y : TopCat.{u}} (œÜ : X ‚ü∂ Y) (hœÜ : IsWeakHomotopyEquiv œÜ.hom) :
    IsCompressible (diskBoundaryInclusion n) <| ofHom <| MapCyl.domInclFromTop œÜ := by
  induction n with
  | zero =>
      have x := hœÜ.left.some
      replace hœÜ := isIso_inducedPointedHom_of_isWeakHomotopyEquiv hœÜ 0
      have hbi : Function.Bijective <|
          RelHomotopyGroup.iStar 0 (MapCyl œÜ) (MapCyl.top œÜ) (MapCyl.domInclToTop œÜ x) :=
        RelHomotopyGroup.bijective_iStar_mapCyl_of_isIso 0 œÜ x (hœÜ x)
      exact isCompressible_zero_subtype_val_of_bijective_iStar_zero _ _ _ hbi
  | succ n =>
      have hpi a : Nonempty <| Unique <| œÄÔπç (n + 1) (MapCyl œÜ) (MapCyl.top œÜ) a := by
        let x := (TopCat.MapCyl.domHomeoTop œÜ).invFun a
        convert RelHomotopyGroup.unique_pi_mapCyl_of_isWeakHomotopyEquiv n œÜ x hœÜ
        unfold MapCyl.domInclToTop x
        simp only [Equiv.invFun_as_coe, Homeomorph.coe_symm_toEquiv, ContinuousMap.coe_coe,
          Homeomorph.apply_symm_apply]
      convert isCompressible_subtype_val_of_unique_pi n (MapCyl œÜ) (MapCyl.top œÜ) hpi

/-- If `œÜ : X ‚ü∂ Y` is a weak homotopy equivalence,
then the inclusion map `MapCyl.domIncl œÜ` from `X` to the mapping cylinder of `œÜ`
is `n`-compressible for each natural number `n`. -/
theorem isCompressible_mapCyl_domIncl_of_isWeakHomotopyEquiv
    (n : ‚Ñï) {X Y : TopCat.{u}} (œÜ : X ‚ü∂ Y) (hœÜ : IsWeakHomotopyEquiv œÜ.hom) :
    IsCompressible (diskBoundaryInclusion n) <| MapCyl.domIncl œÜ where
  sq_hasLift := fun {F f} sq ‚Ü¶ by
    have com := isCompressible_mapcyl_domInclFromTop_of_isWeakHomotopyEquiv n œÜ hœÜ
    have sq' : CommSq (f ‚â´ (ofHom <| MapCyl.domInclToTop œÜ)) (diskBoundaryInclusion n)
      (ofHom <| MapCyl.domInclFromTop œÜ) F := ‚ü®sq.w‚ü©  -- (domIncl f).hom = (domInclFromTop f).comp (domInclToTop f)
    let l := com.sq_hasLift sq' |>.hasLift.some
    let inv : C(MapCyl.top œÜ, X) := toContinuousMap (MapCyl.domHomeoTop œÜ).symm
    use l.l ‚â´ ofHom inv
    ¬∑ have := congrArg‚ÇÇ CategoryStruct.comp l.fac_left (Eq.refl (ofHom inv))
      convert this using 1
      rw [Category.assoc]
      unfold inv MapCyl.domInclToTop
      ext x : 1
      simp only [hom_comp, hom_ofHom, Homeomorph.symm_comp_toContinuousMap, ContinuousMap.id_comp]
    ¬∑ convert l.H using 2
      rw [Category.assoc]
      congr 1
      ext x
      change ((MapCyl.domIncl œÜ).hom ‚àò inv) x = _
      rw [MapCyl.domIncl_hom_eq_domInclFromTop_comp_domInclToTop]
      unfold inv
      simp only [ContinuousMap.coe_comp, ContinuousMap.coe_coe, Function.comp_apply, hom_ofHom]
      congr 1
      unfold MapCyl.domInclToTop
      simp only [ContinuousMap.coe_coe, Homeomorph.apply_symm_apply]

end TopCat.disk
