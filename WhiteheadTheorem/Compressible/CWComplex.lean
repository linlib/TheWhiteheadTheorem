import WhiteheadTheorem.CWComplex.IProd
import WhiteheadTheorem.HEP.Cofibration
import WhiteheadTheorem.Compressible.Defs

/-!
This file proves that if a map is compressible with respect to
`TopCat.diskBoundaryInclusion n : ‚àÇùîª n ‚ü∂ ùîª n`
(inclusion from the boundary of a disk to the disk),
then it is compressible with respect to the inclusion map
from the `-1`-skeleton of any relative CW-complex to the relative CW-complex.
This is the theorem `IsCompressible.relCWComplex_of_diskBoundaryInclusion`.

Some proofs are similar to the ones in `Mathlib.CategoryTheory.LiftingProperties.Limits`
-/


open CategoryTheory unitInterval

universe u


namespace TopCat

/-- Suppose `j` is compressible w.r.t. `i1`, and `i2` is an isomorphism,
then `j` is compressible w.r.t. `i1 ‚â´ i2`.-/
lemma IsCompressible.of_comp_iso_left
    {A B Z X Y : TopCat.{u}} {i1 : A ‚ü∂ X} {i2 : X ‚ü∂ Z} {j : B ‚ü∂ Y}
    (hcom1 : IsCompressible i1 j) [IsIso i2] :
    IsCompressible (i1 ‚â´ i2) j where
  sq_hasLift := fun {F f} sq ‚Ü¶ by
    have sq1 : CommSq f i1 j (i2 ‚â´ F) := ‚ü®by rw [sq.w, Category.assoc]‚ü©
    let l1 := hcom1.sq_hasLift sq1 |>.hasLift.some
    let L : Z ‚ü∂ B := inv i2 ‚â´ l1.l
    let H : Z ‚ü∂ TopCat.of C(I, Y) := inv i2 ‚â´ l1.curriedH
    refine ‚ü®Nonempty.intro <| LiftStructUpToRelHomotopy.curriedMk L ?_ H ?_ ?_ fun t ‚Ü¶ ?_‚ü©
    ¬∑ rw [Category.assoc, IsIso.hom_inv_id_assoc, l1.fac_left]
    ¬∑ rw [Category.assoc, l1.curriedH_apply_zero, IsIso.inv_hom_id_assoc]
    ¬∑ rw [Category.assoc, l1.curriedH_apply_one]; rfl
    ¬∑ have := l1.curriedH_prop t
      simp_all only [Category.assoc, IsIso.hom_inv_id_assoc, l1, H]

/-- Suppose `i1` is an isomorphism and `j` is compressible w.r.t. `i2`,
then `j` is compressible w.r.t. `i1 ‚â´ i2`.-/
lemma IsCompressible.of_iso_comp_left
    {A B Z X Y : TopCat.{u}} {i1 : A ‚ü∂ X} {i2 : X ‚ü∂ Z} {j : B ‚ü∂ Y}
    [IsIso i1] (hcom2 : IsCompressible i2 j) :
    IsCompressible (i1 ‚â´ i2) j where
  sq_hasLift := fun {F f} sq ‚Ü¶ by
    have sq2 : CommSq (inv i1 ‚â´ f) i2 j F :=
      ‚ü®by rw [Category.assoc, sq.w, Category.assoc, IsIso.inv_hom_id_assoc]‚ü©
    let l2 := hcom2.sq_hasLift sq2 |>.hasLift.some
    let L : Z ‚ü∂ B := l2.l
    let H : Z ‚ü∂ TopCat.of C(I, Y) := l2.curriedH
    refine ‚ü®Nonempty.intro <| LiftStructUpToRelHomotopy.curriedMk L ?_ H ?_ ?_ fun t ‚Ü¶ ?_‚ü©
    . rw [Category.assoc, l2.fac_left, IsIso.hom_inv_id_assoc]
    . rw [l2.curriedH_apply_zero]
    . rw [l2.curriedH_apply_one]
    . rw [Category.assoc, l2.curriedH_prop t, Category.assoc]

/-- Suppose `j` is compressible w.r.t. `i`,
and `i` is isomorphic to `i'` in the arrow category,
then `j` is compressible w.r.t. `i'`.-/
lemma IsCompressible.of_arrow_iso_left
    {A X A' X' B Y : TopCat.{u}} {i : A ‚ü∂ X} {i' : A' ‚ü∂ X'} {j : B ‚ü∂ Y}
    (e : Arrow.mk i ‚âÖ Arrow.mk i') (hcom : IsCompressible i j) :
    IsCompressible i' j := by
  rw [Arrow.iso_w' e]
  exact IsCompressible.of_iso_comp_left <| IsCompressible.of_comp_iso_left hcom

/--
If `j` is compressible w.r.t. `i`, then it is also compressible w.r.t. `‚àê i`.
```
‚àê A -----f----‚Üí B
 |              |
‚àê i             j
 |              |
 ‚Üì              ‚Üì
‚àê X -----F----‚Üí Y
```
TODO: This lemma can be generalized to the case where
each component function of `‚àê A ‚ü∂ ‚àê X` can be different.
-/
lemma IsCompressible.coprod {A B X Y : TopCat.{u}} {i : A ‚ü∂ X} {j : B ‚ü∂ Y}
    (hcom : IsCompressible i j) (cells : Type u) :
    IsCompressible (Limits.Sigma.map fun (_ : cells) ‚Ü¶ i) j where
  sq_hasLift := fun {F f} sq ‚Ü¶ by
    have sq' c : CommSq
        (Limits.Sigma.Œπ (fun _ ‚Ü¶ A) c ‚â´ f) i j
        (Limits.Sigma.Œπ (fun _ ‚Ü¶ X) c ‚â´ F) :=
      ‚ü®by simp only [Category.assoc, sq.w, Limits.Œπ_colimMap_assoc, Discrete.functor_obj_eq_as,
            Discrete.natTrans_app] ‚ü©
    let l c := hcom.sq_hasLift (sq' c) |>.hasLift.some
    let L := Limits.Sigma.desc fun c ‚Ü¶ (l c).l
    let h c := (l c).H.some.toContinuousMap.argSwap.curry
    let H := Limits.Sigma.desc fun c ‚Ü¶ ofHom (h c)
    refine ‚ü®Nonempty.intro <| LiftStructUpToRelHomotopy.curriedMk L ?_ H ?_ ?_ fun t ‚Ü¶ ?_‚ü©
    ¬∑ apply Limits.Sigma.hom_ext
      intro c
      have := (l c).fac_left
      simp_all only [Limits.colimit.map_desc, Limits.colimit.Œπ_desc, Limits.Cocones.precompose_obj_pt,
        Limits.Cofan.mk_pt, Limits.Cocones.precompose_obj_Œπ, NatTrans.comp_app, Discrete.functor_obj_eq_as,
        Functor.const_obj_obj, Discrete.natTrans_app, Limits.Cofan.mk_Œπ_app, l, L]
    ¬∑ apply Limits.Sigma.hom_ext
      intro c
      ext x
      -- have := (l c).H.some.apply_zero x
      simp_all only [hom_comp, ContinuousMap.comp_apply, ContinuousMap.HomotopyWith.apply_zero,
        ContinuousMap.argSwap, ContinuousMap.coe_mk, Limits.colimit.Œπ_desc_assoc, Discrete.functor_obj_eq_as,
        Limits.Cofan.mk_pt, Limits.Cofan.mk_Œπ_app, hom_ofHom, ContinuousMap.curry_apply,
        ContinuousMap.prodSwap_apply, ContinuousMap.Homotopy.coe_toContinuousMap,
        ContinuousMap.Homotopy.apply_zero, l, L, H, h]
    ¬∑ apply Limits.Sigma.hom_ext
      intro c
      ext x
      simp_all only [ContinuousMap.argSwap, hom_comp, ContinuousMap.comp_apply, ContinuousMap.coe_mk,
        Limits.colimit.Œπ_desc_assoc, Discrete.functor_obj_eq_as, Limits.Cofan.mk_pt, Limits.Cofan.mk_Œπ_app,
        hom_ofHom, ContinuousMap.curry_apply, ContinuousMap.prodSwap_apply,
        ContinuousMap.Homotopy.coe_toContinuousMap, ContinuousMap.Homotopy.apply_one, l, L, H, h]
    ¬∑ apply Limits.Sigma.hom_ext
      intro c
      ext a
      have := (l c).H.some.prop t (i a) (Set.mem_range_self a)
      simp_all only [hom_comp, ContinuousMap.comp_apply, ContinuousMap.Homotopy.curry_apply,
        ContinuousMap.HomotopyWith.coe_toHomotopy, ContinuousMap.argSwap, ContinuousMap.coe_mk,
        Limits.colimit.map_desc_assoc, Limits.Cofan.mk_pt, Limits.colimit.Œπ_desc_assoc, Discrete.functor_obj_eq_as,
        Limits.Cocones.precompose_obj_pt, Limits.Cocones.precompose_obj_Œπ, NatTrans.comp_app, Functor.const_obj_obj,
        Discrete.natTrans_app, Limits.Cofan.mk_Œπ_app, Category.assoc, hom_ofHom, ContinuousMap.comp_assoc,
        ContinuousMap.curry_apply, ContinuousMap.prodSwap_apply, ContinuousMap.Homotopy.coe_toContinuousMap,
        Limits.Œπ_colimMap_assoc, l, h, L, H]

/--
Suppose the left square in the diagram below is a pushout square.
If `j` is compressible w.r.t. `Œπ`, then it is also compressible w.r.t. `i`.
```
A' -----œÜ----‚Üí A -----f----‚Üí B
 |             |             |
 Œπ             i             j
 |             |             |
 ‚Üì             ‚Üì             ‚Üì
X' -----Œ¶----‚Üí X -----F----‚Üí Y
```
-/
lemma IsCompressible.pushout {A' A B X' X Y : TopCat.{u}}
    {Œπ : A' ‚ü∂ X'} {i : A ‚ü∂ X} {j : B ‚ü∂ Y}
    {œÜ : A' ‚ü∂ A} {Œ¶ : X' ‚ü∂ X} (po : IsPushout œÜ Œπ i Œ¶)
    (com : IsCompressible Œπ j) : IsCompressible i j where
  sq_hasLift := fun {F f} sq ‚Ü¶ by
    have sq' : CommSq (œÜ ‚â´ f) Œπ j (Œ¶ ‚â´ F) := ‚ü®by rw [Category.assoc, sq.w, po.w_assoc] ‚ü©
    let l' := com.sq_hasLift sq' |>.hasLift.some
    let l   : X  ‚ü∂ B          := po.desc f l'.l l'.fac_left.symm
    let H'  : X' ‚ü∂ of C(I, Y) := l'.curriedH
    let H'' : A  ‚ü∂ of C(I, Y) := PathSpace.homToConstPaths (i ‚â´ F)
    let G   : X  ‚ü∂ of C(I, Y) := po.desc H'' H' <| by
      unfold H'' H'
      ext a' t
      simp only [hom_comp, hom_ofHom, ContinuousMap.comp_apply, ContinuousMap.curry_apply,
        ContinuousMap.coe_mk]
      change (œÜ ‚â´ i ‚â´ F) _ = _
      rw [po.w_assoc]
      have := l'.curriedH_prop' t (Œπ a') (Set.mem_range_self a')
      simp only [ContinuousMap.argSwap, ContinuousMap.coe_mk, ContinuousMap.comp_apply,
        ContinuousMap.prodSwap_apply, ContinuousMap.uncurry_apply, Function.uncurry_apply_pair,
        hom_comp] at this
      convert this.symm
    refine ‚ü®Nonempty.intro <| LiftStructUpToRelHomotopy.curriedMk l ?_ G ?_ ?_ fun t ‚Ü¶ ?_‚ü©
    ¬∑ apply po.inl_desc
    ¬∑ apply po.hom_ext
      ¬∑ rw [po.inl_desc_assoc]; rfl
      ¬∑ rw [po.inr_desc_assoc, l'.curriedH_apply_zero]
    ¬∑ apply po.hom_ext
      ¬∑ rw [po.inl_desc_assoc, po.inl_desc_assoc, sq.w]; rfl
      ¬∑ rw [po.inr_desc_assoc, l'.curriedH_apply_one, po.inr_desc_assoc]
    ¬∑ simp only [po.inl_desc_assoc, G, H'', H', l']; rfl


namespace IsCompressible.relCWComplex

/--
```
 X.sk n ---------------------‚Üí B
   |              f            |
   |                           j
   |                           |
   ‚Üì                     F     ‚Üì
X.sk (n + 1) ----‚Üí X --------‚Üí Y
```
-/
private structure FStruct
    (X : RelCWComplex.{u}) {B Y : TopCat.{u}} (j : B ‚ü∂ Y) (n : ‚Ñï) where
  /-- $f_n$ -/
  f : X.sk n ‚ü∂ B
  /-- $F_n$ -/
  F : X.toTopCat ‚ü∂ Y
  /-- The maps $f_n$ and $F_n$ agree on `X.sk n` -/
  sq : CommSq f (X.skInclusionToNextSk n) j (X.skInclusion (n + 1) ‚â´ F)
  /-- The structure containing the lift `l.l : X.sk (n + 1)  ‚ü∂ B`, i.e., the next `f` -/
  l : LiftStructUpToRelHomotopy sq
  /-- The commutative square for constructing the homotopy from $F_n$ to $F_{n+1}$ -/
  hep_sq : CommSq l.curriedH (X.skInclusion (n + 1)) (PathSpace.eval‚ÇÄ Y) F
  /-- The structure containing the homotopy `hep_l.l` from $F_n$ to $F_{n+1}$ -/
  hep_l : hep_sq.LiftStruct

variable {X : RelCWComplex.{u}} {B Y : TopCat.{u}} {j : B ‚ü∂ Y}
  (jcom_sk : ‚àÄ n, IsCompressible (X.skInclusionToNextSk n) j)
  {F‚ÇÄ : X.toTopCat ‚ü∂ Y} {f‚ÇÄ : X.sk 0 ‚ü∂ B}
  (sq : CommSq f‚ÇÄ (X.skInclusion 0) j F‚ÇÄ)

private noncomputable def F : ‚àÄ n : ‚Ñï, FStruct X j n
  | 0 => by
      let sq' : CommSq f‚ÇÄ (X.skInclusionToNextSk 0) j (X.skInclusion (1) ‚â´ F‚ÇÄ) :=
        ‚ü®by
          rw [sq.w, ‚Üê Category.assoc]
          congr 1
          apply Eq.symm
          exact Limits.colimit.w (Functor.ofSequence fun n ‚Ü¶ (X.attachCells n).inclusion) <|
            homOfLE (by omega : 0 ‚â§ 1) ‚ü©
      let l' := jcom_sk 0 |>.sq_hasLift sq' |>.hasLift.some
      have hep_sq : CommSq l'.curriedH (X.skInclusion 1) (PathSpace.eval‚ÇÄ Y) F‚ÇÄ :=
        ‚ü®l'.curriedH_apply_zero‚ü©
      exact
        { f := f‚ÇÄ
          F := F‚ÇÄ
          sq := sq'
          l := l'
          hep_sq := hep_sq
          hep_l := X.skInclusion_isCofibration 1 |>.hasCurriedHEP Y
            |>.hasLift |>.sq_hasLift hep_sq |>.exists_lift.some }
  | n + 1 => by
      let f' := (F n).l.l
      let F' := (F n).hep_l.l ‚â´ PathSpace.eval‚ÇÅ Y
      let sq' : CommSq f' (X.skInclusionToNextSk (n + 1)) j (X.skInclusion (n + 1 + 1) ‚â´ F') :=
        ‚ü®by
          rw [‚Üê (F n).l.curriedH_apply_one, ‚Üê Category.assoc, ‚Üê Category.assoc]
          congr 1
          rw [RelCWComplex.skInclusionToNextSk_skInclusion_eq, (F n).hep_l.fac_left] ‚ü©
      let l' := jcom_sk (n + 1) |>.sq_hasLift sq' |>.hasLift.some
      have hep_sq : CommSq l'.curriedH (X.skInclusion (n + 1 + 1)) (PathSpace.eval‚ÇÄ Y) F' :=
        ‚ü®l'.curriedH_apply_zero‚ü©
      exact
        { f := f'
          F := F'
          sq := sq'
          l := l'
          hep_sq := hep_sq
          hep_l := X.skInclusion_isCofibration (n + 1 + 1) |>.hasCurriedHEP Y
            |>.hasLift |>.sq_hasLift hep_sq |>.exists_lift.some }

/-- Invoke this definition with `m = 0` and `step = n` to get the homotopy `C(I √ó (X.sk n), Y)`
which is the concatenation of `(n + 1)` homotopies,
where the first one is defined on the interval `[0, 1/2]`, the second on `[1/2, 3/4]`,
the third on `[3/4, 7/8]`, ..., the `n`-th on `[1 - 1/2^(n-1), 1 - 1/2^n]`.
The `(n + 1)`-th homotopy defined on `t ‚àà [1 - 1/2^n, 1]` is constant (independent of `t`).

Note: invoking this definition with `step = 0` gives the last homotopy
in the chain of `(n + 1)` homotopies. -/
private noncomputable def H (n m step : ‚Ñï) (hstep : m + step = n) :
    ContinuousMap.Homotopy
      (X.skInclusion n ‚â´ (F jcom_sk sq m).F).hom
      (X.skInclusion n ‚â´ (F jcom_sk sq n).F).hom :=
  match step with
  | 0 =>
      { toContinuousMap := ContinuousMap.Homotopy.refl (X.skInclusion n ‚â´ (F jcom_sk sq n).F).hom
        map_zero_left x := by subst hstep; rfl
        map_one_left x := by subst hstep; rfl }
      -- hstep ‚ñ∏ ContinuousMap.Homotopy.refl (X.skInclusion n ‚â´ (F jcom_sk sq n).F).hom
      -- ContinuousMap.Homotopy.refl ((F n).f ‚â´ j).hom
  | step + 1 => by
      let Hlow : ContinuousMap.Homotopy
          (X.skInclusion n ‚â´ (F jcom_sk sq m).F).hom
          (X.skInclusion n ‚â´ (F jcom_sk sq (m + 1)).F).hom :=
        { toContinuousMap := (X.skInclusion n ‚â´ (F jcom_sk sq m).hep_l.l).hom.uncurry.argSwap
          map_zero_left x := by
            change (X.skInclusion n ‚â´ (F ..).hep_l.l ‚â´ PathSpace.eval‚ÇÄ _).hom x = _
            rw [(F ..).hep_l.fac_right]
          map_one_left x := rfl }
      let Hhigh : ContinuousMap.Homotopy
          (X.skInclusion n ‚â´ (F jcom_sk sq (m + 1)).F).hom
          (X.skInclusion n ‚â´ (F jcom_sk sq n).F).hom :=
        H n (m + 1) step (by rw [‚Üê hstep, add_comm step 1, add_assoc])
      exact Hlow.trans Hhigh  -- `Hlow` on `[0, 1/2]`

private lemma H_skInclusionToNextSk (n m step : ‚Ñï) (hstep : m + step = n) :
    ‚àÄ x t,
      (H jcom_sk sq (n + 1) m (step + 1) (by omega)).toFun (t, (X.skInclusionToNextSk n).hom x) =
      (H jcom_sk sq n m step hstep).toFun (t, x) :=
  let F := TopCat.IsCompressible.relCWComplex.F jcom_sk sq
  match step with
  | 0 => by
      rw [Nat.add_zero] at hstep
      subst hstep
      intro x t
      simp only [hom_comp, H, ContinuousMap.argSwap, ContinuousMap.coe_mk,
        ContinuousMap.toFun_eq_coe, ContinuousMap.Homotopy.coe_toContinuousMap,
        ContinuousMap.coe_coe, ContinuousMap.Homotopy.refl_apply, ContinuousMap.comp_apply]
      simp only [ContinuousMap.Homotopy.trans_apply, one_div]
      by_cases ht : t.val ‚â§ 2‚Åª¬π
      all_goals simp only [ht, ‚ÜìreduceDIte, hom_comp, ContinuousMap.comp_apply]
      ¬∑ change (X.skInclusionToNextSk m ‚â´ (X.skInclusion (m + 1) ‚â´ (F m).hep_l.l) ‚â´
            PathSpace.evalAt _ _).hom x = (X.skInclusion m ‚â´ (F m).F).hom x
        congr 2
        rw [(F m).hep_l.fac_left, (F m).l.curriedH_prop]
        rw [‚Üê X.skInclusionToNextSk_skInclusion_eq m, Category.assoc]
      ¬∑ change (X.skInclusionToNextSk m ‚â´ X.skInclusion (m + 1) ‚â´ (F (m + 1)).F).hom _ =
          (X.skInclusion m ‚â´ (F m).F).hom _
        congr 2
        rw [‚Üê X.skInclusionToNextSk_skInclusion_eq m, Category.assoc, ‚Üê (F m).l.curriedH_prop 1]
        congr 1
        rw [(by rfl : (F (m + 1)).F = (F m).hep_l.l ‚â´ PathSpace.evalAt _ 1)]
        rw [‚Üê Category.assoc, (F m).hep_l.fac_left]
  | step + 1 => by
      intro x t
      unfold H
      simp only [hom_comp, ContinuousMap.argSwap, ContinuousMap.coe_mk, ContinuousMap.toFun_eq_coe,
        ContinuousMap.Homotopy.coe_toContinuousMap]
      simp only [ContinuousMap.Homotopy.trans_apply, one_div]
      by_cases ht : t.val ‚â§ 2‚Åª¬π
      all_goals simp only [ht, ‚ÜìreduceDIte]
      ¬∑ change ((X.skInclusionToNextSk n ‚â´ X.skInclusion (n + 1) ‚â´ (F m).hep_l.l).hom x) _ =
          ((X.skInclusion n ‚â´ (F m).hep_l.l).hom x) _
        congr 3
        rw [‚Üê Category.assoc, X.skInclusionToNextSk_skInclusion_eq]
      ¬∑ apply H_skInclusionToNextSk n (m + 1) step (by omega)

end IsCompressible.relCWComplex


/--
Suppose `X` is a relative CW-complex and `j : B ‚ü∂ Y` is a continuous map.
If `j` is `n`-compressible for every natural number `n`,
then it is compressible w.r.t. the inclusion map from
the $(-1)$-skeleton of `X` to `X`, i.e., any map from the pair
`(X, X.sk 0)` to `(Y, B)` is homotopic relative to `X.sk 0` to a map into `B`.
```
X.sk 0 --- f‚ÇÄ ---‚Üí B
  |                |
  i                j
  |                |
  ‚Üì                ‚Üì
  X ----- F‚ÇÄ ----‚Üí Y
```
-/
theorem IsCompressible.relCWComplex_of_diskBoundaryInclusion
    (X : RelCWComplex.{u}) {B Y : TopCat.{u}} (j : B ‚ü∂ Y)
    (jcom : ‚àÄ n, IsCompressible (diskBoundaryInclusion n) j) :
    IsCompressible (X.skInclusion 0) j where
  sq_hasLift := fun {F‚ÇÄ f‚ÇÄ} sq ‚Ü¶ by
    have jcom_sk n : IsCompressible (X.skInclusionToNextSk n) j := by
      apply IsCompressible.of_comp_iso_left
      apply IsCompressible.pushout (X.attachCells n).pushout_isPushout
      apply IsCompressible.coprod
      exact jcom n
    let F := IsCompressible.relCWComplex.F jcom_sk sq
    let H n := IsCompressible.relCWComplex.H jcom_sk sq n 0 n (by omega)
    let ccL : Limits.Cocone (Functor.ofSequence X.skInclusionToNextSk) :=
      { pt := B
        Œπ := NatTrans.ofSequence (fun n ‚Ü¶ (F n).f)
          (fun n ‚Ü¶ by
            simp only [Functor.ofSequence_obj, Functor.const_obj_obj, homOfLE_leOfHom,
              Functor.ofSequence_map_homOfLE_succ, Functor.const_obj_map, Category.comp_id]
            exact (F n).l.fac_left ) }
    let L : X.toTopCat ‚ü∂ B :=
      Limits.colimit.desc (Functor.ofSequence X.skInclusionToNextSk) ccL
    let ccH : Limits.Cocone (Functor.ofSequence X.skInclusionToNextSk) :=
      { pt := TopCat.of C(I, Y)
        Œπ := NatTrans.ofSequence (fun n ‚Ü¶ ofHom (H n).toContinuousMap.argSwap.curry)
          (fun n ‚Ü¶ by
            simp only [Functor.ofSequence_obj, Functor.const_obj_obj, homOfLE_leOfHom,
              Functor.ofSequence_map_homOfLE_succ, ContinuousMap.argSwap, hom_comp,
              ContinuousMap.coe_mk, Functor.const_obj_map, Category.comp_id]
            ext x t
            simp only [hom_comp, hom_ofHom, ContinuousMap.comp_apply, ContinuousMap.curry_apply,
              ContinuousMap.prodSwap_apply, ContinuousMap.Homotopy.coe_toContinuousMap]
            apply IsCompressible.relCWComplex.H_skInclusionToNextSk ) }
    let H' : X.toTopCat ‚ü∂ TopCat.of C(I, Y) :=
      Limits.colimit.desc (Functor.ofSequence X.skInclusionToNextSk) ccH
    refine ‚ü®Nonempty.intro <| LiftStructUpToRelHomotopy.curriedMk L ?_ H' ?_ ?_ fun t ‚Ü¶ ?_‚ü©
    ¬∑ apply Limits.colimit.Œπ_desc
    any_goals
      apply Limits.colimit.hom_ext
      intro n
      rw [‚Üê Category.assoc, Limits.colimit.Œπ_desc]
      unfold ccH
      simp only [Functor.ofSequence_obj, ContinuousMap.argSwap, hom_comp, ContinuousMap.coe_mk,
        Functor.const_obj_obj, homOfLE_leOfHom, Functor.const_obj_map, id_eq, hom_ofHom,
        ContinuousMap.comp_apply, ContinuousMap.curry_apply, ContinuousMap.prodSwap_apply,
        ContinuousMap.Homotopy.coe_toContinuousMap, eq_mpr_eq_cast, NatTrans.ofSequence_app]
    ¬∑ change _ = X.skInclusion n ‚â´ _
      ext x
      simp only [hom_comp, hom_ofHom, ContinuousMap.comp_apply, ContinuousMap.coe_mk,
        ContinuousMap.curry_apply, ContinuousMap.prodSwap_apply,
        ContinuousMap.Homotopy.coe_toContinuousMap, ContinuousMap.Homotopy.apply_zero]
      rfl
    ¬∑ rw [‚Üê Category.assoc, Limits.colimit.Œπ_desc]
      unfold ccL
      simp only [Functor.ofSequence_obj, Functor.const_obj_obj, homOfLE_leOfHom,
        Functor.const_obj_map, id_eq, eq_mpr_eq_cast, NatTrans.ofSequence_app]
      ext x
      simp only [hom_comp, hom_ofHom, ContinuousMap.comp_apply, ContinuousMap.coe_mk,
        ContinuousMap.curry_apply, ContinuousMap.prodSwap_apply,
        ContinuousMap.Homotopy.coe_toContinuousMap, ContinuousMap.Homotopy.apply_one]
      change (X.skInclusion n ‚â´ (F n).F).hom _ = ((F n).f ‚â´ j).hom _
      congr 2
      rw [(F n).sq.w, ‚Üê Category.assoc, X.skInclusionToNextSk_skInclusion_eq]
    ¬∑ nth_rw 1 [RelCWComplex.skInclusion]
      rw [‚Üê Category.assoc, Limits.colimit.Œπ_desc]
      unfold ccH
      simp only [ContinuousMap.argSwap, hom_comp, ContinuousMap.coe_mk, Functor.ofSequence_obj,
        Functor.const_obj_obj, homOfLE_leOfHom, Functor.const_obj_map, id_eq, hom_ofHom,
        ContinuousMap.comp_apply, ContinuousMap.curry_apply, ContinuousMap.prodSwap_apply,
        ContinuousMap.Homotopy.coe_toContinuousMap, eq_mpr_eq_cast, NatTrans.ofSequence_app]
      ext x
      simp only [hom_comp, hom_ofHom, ContinuousMap.comp_apply, ContinuousMap.coe_mk,
        ContinuousMap.curry_apply, ContinuousMap.prodSwap_apply,
        ContinuousMap.Homotopy.coe_toContinuousMap]
      rfl

end TopCat
